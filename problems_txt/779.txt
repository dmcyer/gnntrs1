(FUN
  app : Ar<a_b> -> a -> b
  lam : (a -> b) -> Ar<a_b>
  case : Sum<a_b> -> (a -> c) -> (b -> c) -> c
  inl : a -> Sum<a_b>
  inr : b -> Sum<a_b>
  pi1 : Pr<a_b> -> a
  pi2 : Pr<a_b> -> b
  pair : a -> b -> Pr<a_b>
)
(VAR
  x: a
  y: b
  X: a
  Y: b
  Z: Sum<a_b>
  F: a -> c
  G: b -> c
  H: Sum<a_b> -> c
  M : a -> b
  N : a
  S : a
  T : b
  U : Pr<a_b>
)
(RULES
  app(lam(\x.M x),N) -> M N,
  case(inl(X), \x.F x , \y.G y ) -> F X,
  case(inr(Y), \x.F x , \y.G y ) -> G Y,
  case(Z,\x.H (inl x), \y.H (inr y)) -> H Z,
  pi1(pair(S,T)) -> S,
  pi2(pair(S,T)) -> T,
  pair(pi1(U),pi2(U)) -> U
)
(COMMENT
[145] subsystem of Definition 2.4; part of Cops #780
submitted by: Makoto Hamana
)
